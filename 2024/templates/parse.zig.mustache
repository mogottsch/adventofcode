const std = @import("std");
const path = @import("common").path;
const log = std.log;

pub const Input = struct {
    numbers: []u64,

    allocator: std.mem.Allocator,

    pub fn deinit(self: *Input) void {
        self.allocator.free(self.numbers);
    }
};

pub fn parseFile(allocator: std.mem.Allocator, filename: []const u8) !Input {
    const filepath = try path.buildPath(allocator, filename);
    defer allocator.free(filepath);
    const file = try std.fs.openFileAbsolute(filepath, .{});
    defer file.close();
    const content = try file.readToEndAlloc(allocator, std.math.maxInt(usize));
    defer allocator.free(content);

    var count: usize = 0;
    for (content) |c| {
        if (c == '\n') count += 1;
    }

    const numbers = try allocator.alloc(u64, count);
    errdefer allocator.free(numbers);

    var line_iterator = std.mem.splitScalar(u8, content[0 .. content.len - 1], '\n');
    var i: usize = 0;
    while (line_iterator.next()) |line| : (i += 1) {
        numbers[i] = std.fmt.parseInt(u64, line, 10) catch |err| {
            log.err("Failed to parse line '{s}': {}", .{ line, err });
            return err;
        };
    }

    return Input{ .numbers = numbers, .allocator = allocator };
}
